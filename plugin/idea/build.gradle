plugins {
    id 'java'
    id 'org.jetbrains.intellij' version '0.4.9'
    id 'org.jetbrains.grammarkit' version '2019.1'
}

apply plugin: 'org.jetbrains.grammarkit'
import org.jetbrains.grammarkit.tasks.*

group 'com.neuroshok'
version '1.0'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

// See https://github.com/JetBrains/gradle-intellij-plugin/
intellij {
    //version 'CL-2019.1.3' // generate parser with this version
    version 'CL-191-EAP-SNAPSHOT' // eap snapshot for clion, 191 for 2019.1
}
patchPluginXml {
    changeNotes """
      Add change notes here.<br>
      <em>most HTML tags may be used</em>"""
}

task generateNglLexer(type: GenerateLexer) {
    // source flex file
    source = "$projectDir/src/main/java/ngl/ngl.flex"

    // target directory for lexer
    targetDir = "$projectDir/src/main/java/ngl/"

    // target classname, target file will be targetDir/targetClass.java
    targetClass = "ngl_lexer"

    intellij {
        version = '2019.1.3' // generate parser with this version
    }
}

task generateNglParser(type: GenerateParser) {
    // source bnf file
    source = "$projectDir/src/main/java/ngl/ngl.bnf"

    // optional, task-specific root for the generated files. Default: none
    targetRoot = "$projectDir/src/main/java/"

    // path to a parser file, relative to the targetRoot
    pathToParser = 'ngl/parser/ngl_parser.java'

    // path to a directory with generated psi files, relative to the targetRoot
    pathToPsiRoot = 'psi'

    intellij {
        version = '2019.1.3' // generate parser with this version
    }

}

compileJava {
    dependsOn generateNglParser
    dependsOn generateNglLexer
}

buildSearchableOptions.enabled = false